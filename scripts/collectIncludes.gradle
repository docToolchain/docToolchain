//tag::collectIncludes[]
import static groovy.io.FileType.*
import java.security.MessageDigest

task collectIncludes(
        description: 'collect all ADRs as includes in one file',
        group: 'docToolchain'
) {
    doFirst {
        new File(targetDir, '_includes').mkdirs()
    }
    doLast {
        //let's search the whole project for files, not only the docs folder
        //could be a problem with node projects :-)
        if (docDir.startsWith('.')) {
            docDir = file(new File(projectDir, docDir).canonicalPath)
        }
        logger.info "docToolchain> docDir: ${docDir}"
        if (scanDir.startsWith('.')) {
            scanDir = file(new File(docDir, scanDir).canonicalPath)
        }
        logger.info "docToolchain> scanDir: ${scanDir}"
        
        logger.info "docToolchain> includeRoot: ${includeRoot}"

        if (includeRoot.startsWith('.')) {
            includeRoot = file(new File(docDir, includeRoot).canonicalPath)
        }
        logger.info "docToolchain> includeRoot: ${includeRoot}"

        File sourceFolder = scanDir
        println "sourceFolder: " + sourceFolder.canonicalPath
        def collections = [:]
        sourceFolder.traverse(type: FILES) { file ->
            if (file.name ==~ '^[A-Z]{3}-.*[.](ad|adoc|asciidoc)$') {
                def type = file.name[0..2]
                if (!collections[type]) {
                    collections[type] = []
                }
                println "file: " + file.canonicalPath
                println "corrected file: " + (file.canonicalPath - scanDir.canonicalPath)[1..-1]
                collections[type] << (file.canonicalPath - scanDir.canonicalPath)[1..-1]
            }
        }
        println "targetDir - docDir: " + (targetDir - docDir)
        println "targetDir - includeRoot: " + (targetDir - includeRoot)
        def pathDiff = '../' * ((targetDir - docDir)
                                    .replaceAll('^/','')
                                    .replaceAll('/$','')
                                    .replaceAll("[^/]",'').size()+1)
        
        println "pathDiff: " + pathDiff
        collections.each { type, fileNames ->
            def outFile = new File(targetDir+'/_includes', type+'_includes.adoc')
            outFile.write("// this is autogenerated\n")
            fileNames.each { fileName ->
                outFile.append ("include::../"+pathDiff+fileName.replace("\\", "/")+"[]\n\n")
            }
        }
    }
}


//end::collectIncludes[]

