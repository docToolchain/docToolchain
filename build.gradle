/*
 * This build file is part of the docToolchain
 */

plugins {
    id "org.asciidoctor.convert" version "2.4.0"
    id "org.aim42.htmlSanityCheck" version "1.1.3"
    id "com.github.ben-manes.versions" version "0.27.0"
    id "org.openapi.generator" version "4.3.1"
//    id 'org.jbake.site' version '5.0.0'
}

apply plugin:'groovy'

dependencies {
    testImplementation(
            ('junit:junit:4.12'),
            ('org.spockframework:spock-core:1.3-groovy-2.5'),
            ('com.athaydes:spock-reports:1.6.2'),
            //('org.slf4j:slf4j-api:1.7.13'),
            //('org.slf4j:slf4j-simple:1.7.13'),
            gradleTestKit()
    )
}
if (project.name!=rootProject.name) {
    // disable all tasks which are not docToolchain tasks
    // this avoids for example that task 'check' runs if invoked in main project
    gradle.taskGraph.whenReady {
        tasks.each { task ->
            if (task.group in ['docToolchain', 'Documentation', 'docToolchain helper']
                    || task.name in ['htmlSanityCheck', 'streamingExecute', 'tasks', 'clean']) {
                task.enabled = true
            } else {
                task.enabled = false
            }
        }
    }
    if (docDir=='.') {
        throw new Exception("""
You are running docToolchain as git submodule.
That is great!
Currently, it is still configured to build its own manual,
because the property docDir points to docToolchain itself.
Please configure docToolchain to point to your doc-sources.
You can do so by specifying it in your build.gradle or on the
command line. Example:
./gradlew generateHTML -PdocDir=../.
see https://docs-as-co.de/getstarted/tutorial2 for more details.
        """)
    }
}

//all available docToolchain modules
//the first is mandatory
apply from: 'scripts/AsciiDocBasics.gradle'
//the following are optional
apply from: 'scripts/fixEncoding.gradle'
apply from: 'scripts/prependFilename.gradle'
apply from: 'scripts/exportEA.gradle'
apply from: 'scripts/exportPPT.gradle'
apply from: 'scripts/exportVisio.gradle'
apply from: 'scripts/exportChangelog.gradle'
apply from: 'scripts/exportContributors.gradle'
apply from: 'scripts/exportJiraIssues.gradle'
apply from: 'scripts/exportJiraSprintChangelog.gradle'
apply from: 'scripts/exportExcel.gradle'
apply from: 'scripts/exportMarkdown.gradle'
apply from: 'scripts/pandoc.gradle'
apply from: 'scripts/publishToConfluence.gradle'
apply from: 'scripts/htmlSanityCheck.gradle'
apply from: 'scripts/collectIncludes.gradle'
apply from: 'scripts/exportMetrics.gradle'
apply from: 'scripts/exportOpenApi.gradle'

// let's set a defaultTask for convenience
//defaultTasks 'exportChangeLog','exportJiraIssues','asciidoctor'
defaultTasks 'exportMarkdown', 'exportChangeLog', 'exportContributors', 'generateHTML', 'htmlSanityCheck'
